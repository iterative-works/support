<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:fo="http://www.w3.org/1999/XSL/Format" xmlns:ui="https://ui.iterative.works/form"
  version="1.0">
  <xsl:output method="xml" indent="yes" encoding="UTF-8" />
  <xsl:template match="/">
    <fo:root language="CS" font-family="Liberation Sans">
      <fo:layout-master-set>
        <fo:simple-page-master master-name="A4-portrait" page-height="29.7cm" page-width="21.0cm"
          margin="2cm">
          <fo:region-body />
        </fo:simple-page-master>
      </fo:layout-master-set>
      <fo:page-sequence master-reference="A4-portrait">
        <fo:flow flow-name="xsl-region-body">
          <xsl:call-template name="header" />
          <xsl:apply-templates select="//ui:form" />
          <xsl:call-template name="signatures" />
        </fo:flow>
      </fo:page-sequence>
    </fo:root>
  </xsl:template>
  <xsl:attribute-set name="th">
    <xsl:attribute name="background-color">#DFDFDF</xsl:attribute>
    <xsl:attribute name="font-weight">bold</xsl:attribute>
    <xsl:attribute name="font-size">10pt</xsl:attribute>
    <xsl:attribute name="border">1pt solid black</xsl:attribute>
    <xsl:attribute name="padding">4pt</xsl:attribute>
  </xsl:attribute-set>
  <xsl:attribute-set name="td">
    <xsl:attribute name="font-size">10pt</xsl:attribute>
    <xsl:attribute name="border">1pt solid black</xsl:attribute>
    <xsl:attribute name="padding">4pt</xsl:attribute>
  </xsl:attribute-set>
  <xsl:attribute-set name="table">
    <xsl:attribute name="border">2pt solid black</xsl:attribute>
    <xsl:attribute name="table-layout">fixed</xsl:attribute>
    <xsl:attribute name="width">100%</xsl:attribute>
  </xsl:attribute-set>
  <xsl:template match="ui:form">
    <fo:table text-align="center" font-size="22pt" font-weight="bold" border="2pt solid black"
      background-color="#6281C6" margin-top="10pt" width="100%" table-layout="fixed">
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell>
            <fo:block padding="4pt">
              <xsl:value-of select="@title" />
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
    <xsl:apply-templates
      select="*" />
  </xsl:template>
  <xsl:template match="ui:section">
    <xsl:if test="@title">
      <fo:block text-align="center" font-size="16pt" font-weight="bold" border="1pt solid black"
        background-color="#A4CBFA" margin-top="10pt" padding-top="4pt" padding-bottom="4pt"
        text-transform="uppercase">
        <xsl:value-of select="@title" />
        <xsl:if test="@subtitle">
          <fo:block font-size="12pt" font-weight="normal" text-transform="none">
            <xsl:value-of select="@subtitle" />
          </fo:block>
        </xsl:if>
      </fo:block>
    </xsl:if>
    <xsl:apply-templates
      select="*" />
  </xsl:template>
  <xsl:template match="ui:labeledField">
    <fo:table table-layout="fixed" width="100%">
      <fo:table-column column-width="40%" column-number="1" />
      <fo:table-column column-width="60%" column-number="2" />
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>
              <xsl:value-of select="@label" />
            </fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>
              <xsl:value-of select="ui:inputValue/text()" />
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
  <xsl:template match="ui:choiceField">
    <fo:table table-layout="fixed" width="100%">
      <fo:table-column column-width="40%" column-number="1" />
      <fo:table-column column-width="60%" column-number="2" />
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>
              <xsl:value-of select="@label" />
            </fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>
              <xsl:value-of select="ui:inputValue/text()" />
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
  <xsl:template match="ui:block">
    <xsl:apply-templates select="*" />
  </xsl:template>
  <xsl:template match="ui:headerCell">
    <fo:table border="1pt solid black" table-layout="fixed" width="100%" background-color="#DFDFDF"
      font-size="10pt" font-weight="bold">
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell padding="4pt">
            <fo:block linefeed-treatment="preserve" font-size="10pt" margin-bottom="2pt">
              <xsl:value-of select="text()" />
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
  <xsl:template match="ui:dataCell">
    <fo:table border="1pt solid black" table-layout="fixed" width="100%">
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell padding="4pt">
            <fo:block linefeed-treatment="preserve" font-size="10pt" margin-bottom="2pt">
              <xsl:value-of select="text()" />
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
  <xsl:template name="header">
    <fo:table xsl:use-attribute-sets="table" border-bottom-width="1pt">
      <fo:table-column column-width="40%" column-number="1" />
      <fo:table-column column-width="60%" column-number="2" />
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell border="1pt solid black">
            <fo:block>
              <fo:external-graphic src="url('images/header_logo.png')" content-width="50mm"
                content-height="auto" />
            </fo:block>
          </fo:table-cell>
          <fo:table-cell font-weight="bold" font-size="13pt" text-align="center"
            display-align="center" padding="10pt">
            <fo:block>OZNÁMENÝ SUBJEKT - NB 1383</fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
    <fo:table
      xsl:use-attribute-sets="table" border-top-width="1pt">
      <fo:table-column column-width="25%" column-number="1" />
      <fo:table-column column-width="28%" column-number="2" />
      <fo:table-column column-width="8%" column-number="3" />
      <fo:table-column column-width="39%" column-number="4" />
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Název:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>Český metrologický institut</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Adresa:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>ČMI Medical, Hvožďanská 2053/3, 148 00 Praha - Chodov</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Sídlo:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>Okružní 31/772, 638 00 Brno</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>IČO / DIČ:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>00177016 / CZ00177016</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Email:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>
              <fo:basic-link external-destination="url('mailto:medical@cmi.cz')" color="#2A61BB">
    medical@cmi.cz
              </fo:basic-link>
            </fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Datová schránka:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" number-columns-spanned="3">
            <fo:block>65msw6w</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Číslo účtu v Kč:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>198139621/0710</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>IBAN:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>CZ18 0710 0000 0001 9813 9621</fo:block>
          </fo:table-cell>
        </fo:table-row>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Číslo účtu v EUR:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>34534-198139621/0710</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="th" width="5mm">
            <fo:block>IBAN:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td">
            <fo:block>CZ88 0710 0345 3401 9813 9621</fo:block>
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
  <xsl:template name="signatures">
    <fo:block text-align="center" font-size="16pt" font-weight="bold" border="1pt solid black"
      background-color="#A4CBFA" margin-top="10pt" padding-top="4pt" padding-bottom="4pt"
      text-transform="uppercase">
      PODPIS PRACOVNÍKA
    </fo:block>
    <fo:table
      xsl:use-attribute-sets="table" border-top-width="1pt">
      <fo:table-column column-width="40%" column-number="1" />
      <fo:table-column column-width="60%" column-number="2" />
      <fo:table-body>
        <fo:table-row>
          <fo:table-cell xsl:use-attribute-sets="th">
            <fo:block>Podpis pracovníka odpovědného za zpracování:</fo:block>
          </fo:table-cell>
          <fo:table-cell xsl:use-attribute-sets="td" height="50mm">
            <fo:block />
          </fo:table-cell>
        </fo:table-row>
      </fo:table-body>
    </fo:table>
  </xsl:template>
</xsl:stylesheet>